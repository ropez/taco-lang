
fun test_simple_serialization() {
    rec Foo(x: int)

    assert json(Foo(13)) == """{"x":13}"""
}

fun test_serialization_with_attrs() {
    rec Foo(
        x: int @json(name: "taco")
    )

    assert json(Foo(x: 13)) == """{"taco":13}"""
}

fun test_serialization_in_callback() {
    rec Foo(
        x: int @json(name: "taco")
    )

    points = [Foo(2), Foo(4)]

    assert points.map(fun(p) { json(p) }) == ["""{"taco":2}""", """{"taco":4}"""]
}

fun test_parse_json() {
    input = """{ "a": 123, "b": 456 }""".as_json()

    rec Point(a: int, b: int)
    res = Point::parse(input)

    assert res.is_ok()

    p = res.value()!
    assert "$p" == "Point(a: 123, b: 456)"
}

fun test_parse_json_with_attrs() {
    input = """{ "foo": 123, "bar": 456 }""".as_json()

    rec Point(
        a: int @json(name: "foo")
        b: int @json(name: "bar")
    )
    res = Point::parse(input)

    assert res.is_ok()

    p = res.value()!
    assert "$p" == "Point(a: 123, b: 456)"
}

fun test_parse_invalid_json_error() {
    rec Point(a: int, b: int)

    input = """{ poop }""".as_json()
    res = Point::parse(input)

    assert res.is_err()
    assert res.error().is_some()
    assert res.value().is_none()
}
