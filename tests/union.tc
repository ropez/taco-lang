
fun test_simple_union() {
    union Hero {
        Fred
        Wilma
        Barney
    }

    hero = Hero::Fred

    assert "$hero" == "Fred"
    assert typeof(hero) == "Hero"
}

fun test_comparison() {
    union Hero {
        Fred
        Wilma
        Barney
    }

    hero = Hero::Fred

    assert hero == Hero::Fred
    assert hero != Hero::Wilma
    assert Hero::Wilma != Hero::Barney
}

fun test_union_with_value() {
    union Country {
        Spain
        France
        Italy
    }

    union City {
        Paris
        London
        Madrid
    }

    union VacationPlan {
        StayAtHome
        TravelTo(Country, City)
    }

    assert typeof(VacationPlan::StayAtHome) == "VacationPlan"
    assert typeof(VacationPlan::TravelTo) == "fun(Country, City): VacationPlan"

    plan = VacationPlan::TravelTo(Country::Spain, City::Madrid)
    assert plan != VacationPlan::StayAtHome
    assert plan != VacationPlan::TravelTo(Country::Spain, City::Paris)
    assert plan == VacationPlan::TravelTo(Country::Spain, City::Madrid)
    assert "$plan" == "TravelTo(Spain, Madrid)"
}

fun test_apply_arguments_from_tuple() {
    union Receiver {
        Person(name: str, age: int)
        Group(name: str)
    }

    args = ("per", 42)
    per = Receiver::Person(=args)

    assert "$per" == "Person(name: per, age: 42)"
}

fun test_pass_variant_as_function() {
    union Receiver {
        Person(name: str, age: int)
        Group(name: str)
    }

    args = [
        ("per", 42)
        ("kari", 36)
    ]

    people = args.map_to(Receiver::Person)

    assert people == [
        Receiver::Person(name: "per", age: 42),
        Receiver::Person(name: "kari", age: 36)
    ]
}

fun test_apply_params() {
    union Union {
        Bar
        Foo(a: int, b: int, c: int, d: int)
    }

    expected = Union::Foo(1, 2, 3, 4)
    assert Union::Foo(1, 2, 3, 4) == expected
    assert Union::Foo(1, 2, 3, d: 4) == expected
    assert Union::Foo(1, 2, d: 4, c: 3) == expected
    assert Union::Foo(1, d: 4, c: 3, b: 2) == expected
    assert Union::Foo(c: 3, d: 4, a: 1, b: 2) == expected
    assert Union::Foo(b: 2, c: 3, d: 4, 1) == expected
    assert Union::Foo(b: 2, 1, c: 3, d: 4) == expected
    assert Union::Foo(d: 4, 1, c: 3, 2) == expected
    assert Union::Foo(1, d: 4, 2, 3) == expected
    assert Union::Foo(1, c: 3, 2, 4) == expected
}
