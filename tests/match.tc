
fun test_basic_pattern_mathing_bool() {
    fun test(b: bool) {
        match b {
            true { "foo" }
            false { "bar" }
        }
    }

    assert test(true) == "foo"
    assert test(false) == "bar"
}

fun test_pattern_mathing_union() {
    union Sign { Rock, Paper, Scissors }

    fun test(sign: Sign) {
        match sign {
            Sign::Rock { "Rock beats Scissors" }
            Sign::Paper { "Paper beats Rock" }
            Sign::Scissors { "Scissors beats Paper" }
        }
    }

    assert test(Sign::Rock) == "Rock beats Scissors"
    assert test(Sign::Scissors) == "Scissors beats Paper"
}

fun test_pattern_mathing_implicit_union() {
    union Sign { Rock, Paper, Scissors }

    fun test(sign: Sign) {
        match sign {
            ::Rock { "Rock beats Scissors" }
            ::Paper { "Paper beats Rock" }
            ::Scissors { "Scissors beats Paper" }
        }
    }

    assert test(Sign::Rock) == "Rock beats Scissors"
    assert test(Sign::Scissors) == "Scissors beats Paper"
}

fun test_pattern_mathing_fallback() {
    fun test(div: int) {
        match div {
            0 { 100000 }
            _ { 100 / div }
        }
    }

    assert test(0) == 100000
    assert test(1) == 100
    assert test(2) == 50
}

fun test_pattern_mathing_variable() {
    fun test(token: str) {
        match token {
            "foo" { true }
            "bar" { true }
            other { other.len() == 1 }
        }
    }

    assert test("foo") == true
    assert test("koko") == false
    assert test("x") == true
}

fun test_pattern_matching_destructure_union() {
    union Country {
        Spain
        France
        Italy
    }

    union City {
        Paris
        London
        Madrid
    }

    union VacationPlan {
        StayAtHome
        TravelTo(Country, City)
    }

    fun test(plan: VacationPlan): str {
        match plan {
            VacationPlan::StayAtHome { "staying home this year" }
            VacationPlan::TravelTo(country, city) { "visiting $city, $country!" }
        }
    }

    assert test(VacationPlan::StayAtHome) == "staying home this year"
    assert test(VacationPlan::TravelTo(Country::France, City::Paris)) == "visiting Paris, France!"
}
